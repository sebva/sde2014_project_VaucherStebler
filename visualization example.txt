cg := CallGraph new.
dict := cg generate: 35.
methods := dict keys.
classes := Set new.
dict values do: [ :each | classes addAll: each. ].
numInvocations := methods collect: [ :e | e incomingInvocations size ].
numLOCm := methods collect: [ :e | e numberOfLinesOfCode ].
numLOCc := classes collect: [ :e | e numberOfLinesOfCode ].
minNumInvocations := numInvocations min.
maxNumInvocations := numInvocations max.
minLOCm := numLOCm min.
maxLOCm := numLOCm max.
minLOCc := numLOCc min.
maxLOCc := numLOCc max.

view := RTView new.
"Let size of squares be between [15 ... 85]"
shapeMethods := RTBox new size: [:e | ((((e numberOfLinesOfCode)-minLOCm) / (maxLOCm - minLOCm)) * 70) + 15]; color: [:e | Color r:0.8 g:0 b:0 alpha: ((((e incomingInvocations size)-minNumInvocations) / (maxNumInvocations - minNumInvocations)) * 0.7) + 0.3]. "Let alpha be between [0.3 ... 1.0]"
shapeClasses := RTEllipse new size: [:e | ((((e numberOfLinesOfCode)-minLOCc) / (maxLOCc - minLOCc)) * 80) + 20]; color: (Color r:0 g:0.8 b:0). "Let size of circles be between [20 ... 100]"

methods := shapeMethods elementsOn: methods.
classes := shapeClasses elementsOn: classes.

view addAll: methods.
view addAll: classes.
RTEdge buildEdgesFromObjects: (dict keys) from: #yourself toAll: [ :e | dict at: e ] using: (RTLine new color: (Color blue alpha: 0.6); width: 5) inView: view.
RTCenteredCircleLayout new initialRadius:300; on: methods.
view @ RTDraggableView.
methods @ RTDraggable.
methods @ RTHighlightable.
methods @ (RTPopup new text: [:e | e asString, ' - LOC: ', e numberOfLinesOfCode asString, ' - NOIC: ', e incomingInvocations size asString, ' - Rank: ', (cg ranks at: e) asString]).
methods @ (RTMenuActivable new action: #inspect).

RTCenteredCircleLayout new initialRadius:500; on: classes.
classes @ RTDraggable.
classes @ RTHighlightable.
classes @ (RTPopup new text: [:e | e asString, ' - LOC: ', e numberOfLinesOfCode asString]).
classes @ (RTMenuActivable new action: #inspect).

"Legend"
legendBuilder := RTLegendBuilder new view: view.
legendBuilder name: 'Top 35 methods' color: (Color r:0.8 g:0 b:0).
legendBuilder name: 'Possible receivers' color: (Color r:0 g:0.8 b:0).
legendBuilder name: 'Brightness of methods: NOIC' color: (Color r:0 g:0 b:0 alpha:0).
legendBuilder name: 'Size: LOC' color: (Color r:0 g:0 b:0 alpha:0).
legendBuilder render.

view